#!/usr/bin/perl -w

use strict;
use CGI;
use Bio::Graphics::Browser2;
use Bio::Graphics::Browser2::Render;
use Bio::Graphics::Browser2::Render::Synteny::HTML;

umask 022;

my $fcgi   = Bio::Graphics::Browser2::Render->fcgi_request;

if ($fcgi) {

    my $FCGI_DONE = 0;
    $SIG{USR1} = $SIG{PIPE} = $SIG{TERM} = sub {
	my $sig  = shift;
	my $time = localtime;
	print STDERR "[$time] [notice] GBrowse FastCGI process caught sig$sig. Exiting... (pid $$)\n";
	$FCGI_DONE = 1;
    };

    my %sys_env = %ENV;
  FCGI_MAIN_LOOP:
    while (!$FCGI_DONE) {
	my $status = $fcgi->Accept;
	next unless $status >= 0;
        %ENV = (%sys_env, %ENV);
	my $globals = Bio::Graphics::Browser2->open_globals;
	CGI->initialize_globals();
        eval {
            Bio::Graphics::Browser2::Render::Synteny::HTML->new($globals)->run();
        }; if( $@ ) {

            die $@ unless ref $@ eq 'ARRAY' && $@->[0] eq 'EXIT';

            $@->[1] == 0
                or warn "WARNING: gbrowse_syn tried to exit with code $@->[1]";
        }
    }
}
else {
    $SIG{TERM} = sub { warn "gbrowse going down..."; CORE::exit 0 };
    my $globals = Bio::Graphics::Browser2->open_globals;
    Bio::Graphics::Browser2::Render::Synteny::HTML->new($globals)->run();
}

exit 0;
